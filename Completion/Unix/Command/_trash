#compdef trash

local variant

_pick_variant -r variant ali='(Rantakari|hasseg)' steven='(Steven|vanZyl)' other --version

case $variant in
  ali) 
    # The hidden options here are options to rm that trash silently (and
    # undocumentedly) ignores. Some options are not made mutually exclusive where
    # they technically could be, for compatibility with aliases, etc.
    _arguments -s -S -A '-*' : \
      '!-'{d,f,i,r,P,R,W} \
      '(: * -F -l -v)-e[empty trash]' \
      '-F[use Finder instead of system API]' \
      '(: * -e -F -s -y)-l[list items in trash]' \
      '(: * -F -l -v)-s[securely empty trash]' \
      '-v[increase output verbosity]' \
      '-y[skip confirmation prompts (with -e or -s)]' \
      '*: :_files'
    return
    ;;
  steven) 
    _arguments -s -S -A '-*' : \
      '(-d --dir)'{-d,--dir}'[remove empty directories]' \
      '(-r --recursive)'{-r,--recursive}'[delete directories and their contents]' \
      '(-v --verbose)'{-v,--verbose}'[print more information]' \
      '(-i --interactive -I --interactive-once -f --force)'{-i,--interactive}'[ask before each deletion]' \
      '(-i --interactive -I --interactive-once -f --force)'{-I,--interactive-once}'[ask before deleting 3 or more files, or deleting recursively]' \
      '(-i --interactive -I --interactive-once -f --force)'{-f,--force}'[don'\''t prompt and ignore errors]' \
      '(- *)--version[output the version and exit]' \
      '(- *)--list[list out the files in the trash]' \
      '(- *)--orphans[list orphaned files in the trash]' \
      '(- *)--restore[restore specified file from the trash]:trashed file:_files -W ${XDG_DATA_HOME\:-$HOME/.local/share}/Trash/files' \
      '(- *)--delete[delete specified file from the trash]:trashed file:_files -W ${XDG_DATA_HOME\:-$HOME/.local/share}/Trash/files' \
      '(- *)--empty[empty the trash bin]' \
      '--rm[permanently delete files]' \
      '(- *)'{-h,--help}'[display help information]' \
      '*: :_files'
    return
    ;;
  *) _default ;;
esac
